# ##############################################################################
#                           CARMA DEPENDENCIES                                 #
# ##############################################################################
# Python and Numpy dependencies
IF (carma_FIND_REQUIRED)
    FIND_PACKAGE(Python3 COMPONENTS Development NumPy QUIET)
    IF (NOT Python3_FOUND)
        FIND_PACKAGE(Python3 COMPONENTS Development.Module NumPy REQUIRED)
    ENDIF()
ELSE ()
    FIND_PACKAGE(Python3 COMPONENTS Development NumPy QUIET)
    IF (NOT Python3_FOUND)
        FIND_PACKAGE(Python3 COMPONENTS Development.Module NumPy)
    ENDIF()
ENDIF ()
IF (Python3_FOUND)
    LIST(APPEND carma_INCLUDE_DIRS ${Python3_INCLUDE_DIRS})
ENDIF ()
IF (Python3_NumPy_FOUND)
    LIST(APPEND carma_INCLUDE_DIRS ${Python3_NumPy_INCLUDE_DIRS})
ENDIF ()

IF (NOT DEFINED PYTHON_EXECUTABLE)
    SET(PYTHON_EXECUTABLE ${Python3_EXECUTABLE})
ENDIF ()

# Armadillo dependency
IF (NOT TARGET armadillo AND NOT TARGET armadillo::armadillo)
    IF (carma_FIND_REQUIRED)
        FIND_PACKAGE(Armadillo REQUIRED)
    ELSE ()
        FIND_PACKAGE(Armadillo)
    ENDIF ()
ENDIF ()
IF (NOT TARGET armadillo::armadillo)
    IF (TARGET armadillo)
        ADD_LIBRARY(armadillo::armadillo ALIAS armadillo)
    ELSEIF (Armadillo_FOUND)
        if(NOT ARMADILLO_INCLUDE_DIRS OR NOT ARMADILLO_LIBRARIES)
            message(FATAL_ERROR "Armadillo_FOUND is true but ARMADILLO_INCLUDE_DIRS or ARMADILLO_LIBRARIES are not set.")
        ENDIF ()
        ADD_LIBRARY(armadillo::armadillo INTERFACE)
        TARGET_INCLUDE_DIRECTORIES(armadillo::armadillo INTERFACE ${ARMADILLO_INCLUDE_DIRS})
        TARGET_LINK_LIBRARIES(armadillo::armadillo INTERFACE ${ARMADILLO_LIBRARIES})
    ENDIF ()
ENDIF ()
IF (TARGET armadillo::armadillo)
    if(NOT ARMADILLO_INCLUDE_DIRS OR NOT ARMADILLO_LIBRARIES)
        message(FATAL_ERROR "armadillo::armadillo is a target but ARMADILLO_INCLUDE_DIRS is not set.")
    ENDIF ()
    LIST(APPEND carma_INCLUDE_DIRS ${ARMADILLO_INCLUDE_DIRS})
ENDIF ()

# pybind11 dependency
IF(NOT TARGET pybind11::pybind11)
    IF (PYBIND11_ROOT_DIR)
        ADD_SUBDIRECTORY(${PYBIND11_ROOT_DIR} pybind11)
    ELSE()
        IF (carma_FIND_REQUIRED)
            FIND_PACKAGE(pybind11 CONFIG REQUIRED)
        ELSE ()
            FIND_PACKAGE(pybind11 CONFIG)
        ENDIF()
    ENDIF()
    IF (pybind11_FOUND)
        LIST(APPEND carma_INCLUDE_DIRS ${pybind11_INCLUDE_DIR})
    ENDIF()
ENDIF()
